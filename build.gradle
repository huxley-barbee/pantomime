plugins {
    id 'groovy'
    id 'java'
    id 'maven-publish'
}

group = 'org.blackmist'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.24.3'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.slf4j:slf4j-reload4j:2.0.17'
    implementation 'org.apache.groovy:groovy-all:4.0.18'
    implementation 'jakarta.activation:jakarta.activation-api:2.0.1'
    implementation 'jakarta.mail:jakarta.mail-api:2.1.3'
    implementation 'org.eclipse.angus:angus-mail:2.0.3'
    implementation 'com.mysql:mysql-connector-j:9.3.0'
    testImplementation 'junit:junit:4.13.2'
}

jar {
    archiveBaseName.set('pantomime')
    from 'LICENSE'
}

test {
    systemProperties['log4j.debug'] = ''
    classpath += files('etc')

    doFirst {
        if (!System.getenv("CI")) {  // Only load DB locally
            println "Loading MySQL schema locally..."
            exec {
                commandLine 'mysql', '-h', '127.0.0.1', '-u', 'root', 'pantomime'
                standardInput = file('data/pantomime.sql').newInputStream()
                ignoreExitValue = false
            }
        } else {
            println "Running in CI â€” assuming MySQL schema is already loaded."
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'com.github.huxley-barbee'
            artifactId = 'pantomime'
            version = '1.0'

            pom {
                name = 'Pantomime'
                description = 'A robust, memory-efficient MIME library for Java'
                url = 'https://github.com/huxley-barbee/pantomime'
                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://www.mozilla.org/MPL/2.0/'
                    }
                }
                scm {
                    url = 'https://github.com/huxley-barbee/pantomime'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/huxley-barbee/pantomime")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

